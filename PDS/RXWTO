RXWTO    title 'REXX function to write a message to the console'
*--------------------------------------------------------------------*
* Program Name  : RXWTO
*
* Language      : Assembler
* LinkEdit Attr : RENT REUS REFR AC(0)
* AMODE         : 31
* RMODE         : ANY
*
*--------------------------------------------------------------------*
* Program Function
*
* External REXX function to write a message on the console
*
* Syntax
*
* y = rxwto('Your message')
*
* Problems
*  Contact Alfred Nykolyn  apn@istar.ca
*
*--------------------------------------------------------------------*
*
rxwto    csect
rxwto    amode 31
rxwto    rmode any
         save  (14,12)
         lr    r12,r15
         using rxwto,r12

         lr    r2,r1
         using efpl,r2                addr REXX func parms

         la    r0,$worklen
         getmain RU,lv=(0),loc=ANY

         st    r1,forward(,r13)
         st    r13,backward(,r1)
         lr    r13,r1
         using workarea,r13

         l     r3,efpleval            addr of pointer to eval block
         l     r3,0(,r3)              REXX eval block addr
         using evalblock,r3

         l     r4,efplarg             REXX arg list
         using argtable_entry,r4      address arg table

         icm   r5,b'1111',argtable_argstring_ptr
         bnp   done                   no args

         icm   r6,b'1111',argtable_argstring_length
         bnp   done

         la    r15,125
         cr    r6,r15                 longer than 125?
         bnh   *+4+2
         lr    r6,r15                 truncate
         sth   r6,messageln           save length
         bctr  r6,0
         ex    r6,movemsg             mvc message(*-*),0(r5)
         la    r7,messageln
         mvc   wtom($wtoplen),wtop
         xr    r0,r0
         wto   text=(r7),mf=(E,wtom)

done     equ   *
         la    r0,1
         st    r0,evalblock_evlen     Return 0 (REXX function!!)
         mvi   evalblock_evdata,c'0'
         lr    r11,r13
         l     r13,backward(,r13)     R13 -> old savearea
         la    r0,$worklen
         freemain RU,lv=(0),a=(r11)

         return (14,12),rc=0
*
*-------------------------------------------------------------------*
* Executed Instructions                                             *
*-------------------------------------------------------------------*
         ds    0d
movemsg  mvc   message(*-*),0(r5)
*
*-------------------------------------------------------------------*
* Constants, Variables and DSECTs                                   *
*-------------------------------------------------------------------*
         ds    0d
noresult dc    x'80000000'

wtop     wto   text=,routcde=(2),desc=(6),mf=L
$wtoplen equ   *-wtop

         ltorg

workarea dsect
savearea ds    18f                    Savearea
forward  equ   8
backward equ   4

wtom       ds    xl($wtoplen)
messageln  ds    h
message    ds    cl125                Message
$worklen   equ   *-workarea

         irxefpl                      REXX mapping macros
         irxevalb
         irxargtb

         yregs
         end
