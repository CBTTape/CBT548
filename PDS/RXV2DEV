         Title 'RXV2Dev - Return Device Number from VolSer'
*
*  This Rexx function returns the device number (DevN) that contains
*  the specified VolSer.
*
*  Invocation (from Rexx):
*    devn = RXV2Dev(volser)
*
*  where volser is a 6 character volume label
*
*  Returns:
*     0  Everything OK
*    -1  Parameter error
*    -2  UCBScan error
*    -3  VolSer not found
*
*  Attributes:
*    Amode 31
*    Rmode any
*    Reentrant, Refreshable, Reusable, AC(0)
*
* Problems:
*    Contact Alfred Nykolyn  apn@istar.ca
*
RXV2Dev  csect
RXV2Dev  amode 31
RXV2Dev  rmode any
RXV2Dev  csect
         save (14,12)
         lr   r12,r15
         using RXV2Dev,r12

         la    r0,$vol2devln
         lr    r10,r1
         getmain RU,lv=(0),loc=ANY

         st    r1,forward(,r13)
         st    r13,backward(,r1)
         lr    r13,r1
         using vol2devd,r13
*
*   Get VolSer
*
         lr    r2,r10             * R2 -> EFPL
         using efpl,r2

         l     r3,efpleval
         l     r3,0(,r3)          * R3 -> Evalblock
         using evalblock,r3

         l     r4,efplarg         * R4 -> arg list
         using argtable_entry,r4

         ltr   r4,r4              * Any parms?
         bz    badparm

         icm   r5,b'1111',argtable_argstring_ptr
         bnp   badparm

         la    r6,6
         l     r7,argtable_argstring_length
         cr    r7,r6              * Wrong length?
         bne   badparm

         mvc   VolSer,0(r5)       * Save VolSer
         oc    VolSer,blanks      * To upper case
         drop  r2,r4
*
*  Build UCBSCAN Parameters
*
         la    r9,ucbscnwk        * r9  -> UCBScan workarea
         xc    ucbscnwk,ucbscnwk
         la    r8,ucbarea         * r8  -> UCB area
*
*  Issue UCBSCAN
*
         ucbscan COPY,                                                 +
               volser=VolSer,                                          +
               workarea=(r9),                                          +
               ucbarea=(r8),                                           +
               range=ALL,                                              +
               dynamic=YES,                                            +
               devclass=DASD,                                          +
               mf=(E,ucbscanl)

         ltr   r10,r15
         bnz   badscan            * OK?

         lh    r0,4(,r8)          * r0 = DevN (Should use mapping)
         sth   r0,devc
         unpk  wa(5),devc(3)
         tr    wa(4),hextab-c'0'
         mvc   evalblock_evdata(4),wa       * Insert DevN
         la    r0,4
         st    r0,evalblock_evlen           * Save DevN length

done     equ   *
         la    r0,$vol2devln
         lr    r11,r13
         l     r13,backward(,r13)
         freemain RU,lv=(0),a=(r11)

         return (14,12),rc=0
*
*   Errors
*
badparm  equ  *
         mvc   evalblock_evdata(2),=c'-1'
         la    r0,2
         st    r0,evalblock_evlen
         wto   mf=(E,badpmsg)

         b     done

badscan  equ   *
         sll   r10,8
         or    r10,r0             * R10 = RC+RSN
         cl    r10,NotFound
         bne   ucbscan_err

         mvc   wtos(msg2ln),msg2
         mvc   wtos+$i1(l'VolSer),VolSer
         wto   mf=(E,wtos)

         mvc   evalblock_evdata(2),=c'-3'
         la    r0,2
         st    r0,evalblock_evlen
         b     done

ucbscan_err     equ  *
         cvd   r15,wa             * R15 = RC
         mvc   wtos(msg1ln),msg1
         mvc   wtos+$i1(l'VolSer),VolSer
         mvc   wtos+$i2(l'edmaskt),edmaskt
         mvc   wtos+$i3(l'edmaskt),edmaskt
         ed    wtos+$i2(l'edmaskt),wa+6

         cvd   r0,wa              * R0 = rsn
         ed    wtos+$i3(l'edmaskt),wa+6
         wto   mf=(E,wtos)        * Message 1

         mvc   evalblock_evdata(2),=c'-2'
         la    r0,2
         st    r0,evalblock_evlen
         b     done
*
*   Constants
*
         ds    0d
blanks   dc    cl8' '
NotFound dc    al4(4*256+1)       * RC+RSN volser not found
hextab   dc    c'0123456789abcdef'
edmaskt  dc    x'40202120'

$i1      equ   13
$i2      equ   38
$i3      equ   47
*                     1111111111222222222223333333334444444444555555555
*               4567890123456789012345678901234567890123456789012345678
msg1     wto   'RXV2Dev: 012345 UCBSCAN error: RC=0123 RSN=0123',      *
               routcde=11,mf=L
msg1ln   equ   *-msg1

msg2     wto   'RXV2Dev: 012345 Volume not found!!',                   *
               routcde=11,mf=L
msg2ln   equ   *-msg2

badpmsg  wto   'RXV2Dev: Invalid paramter',                            *
               routcde=11,mf=L

         ds    0d
         ltorg
*
*  UCBSCAN Workarea and Parameter List
*
vol2devd dsect
savearea ds    18f
forward  equ   8
backward equ   4

wa       ds    d
VolSer   ds    cl6
devc     ds    xl2
         ds    0f
ucbarea  ds    xl48
ucbscnwk ds    xl100

         ucbscan  mf=(L,ucbscanl,0D)

         ds    0d
wtos     ds    xl128
$vol2devln     equ  *-vol2devd

         irxefpl dsect=YES
         irxevalb
         irxargtb
         yregs
         end
